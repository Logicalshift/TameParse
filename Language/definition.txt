///
/// Parser Language Definition
///
/// Created by Andrew Hunter on 21/05/2011
///

language ParserLanguage {
	/// Symbols used by the lexer
	lexer-symbols {
		// Commonly used character sets
		letter 			= /[A-Za-z\-]/
		digit 			= /[0-9]/
		alphanumeric 	= /({letter}|{digit})/
		whitespace		= /[ \n\r]/
		anycharacter	= /[^\n\r]/
		
		// Identifiers, regular expressions
		identifier		= /{letter}{alphanumeric}*/
		regex			= /\/([^\/]|(\\\/))*\//
	}

	/// Lexical symbols
	lexer {
		identifier		= /{identifier}/
		nonterminal		= /\<{identifier}\>/
		regex			= /{regex}/
		string			= /"([^"]|(\\"))*"/
		character		= /'(.|\\.)'/
	}
	
	/// Ignored symbols
	ignore {
		newline			= /[\n\r]/
		whitespace		= /[ \t]/
		comment			= /\/\/({anycharacter}*)/
	}
	
	/// Keywords
	/// (We use very few keywords, to extend the range of identifiers available)
	keywords {
	}
	
	/// The grammar itself
	grammar {
	
		//
		// The top-level definitions
		//
		
		<Parser-Language>		= (<TopLevel-Block>)*
		
		<TopLevel-Block>		= <Language-Block>
								| <Import-Block>
		
		<Language-Block>		= language identifier (<Language-Inherits>)? '{' (<Language-Definition>)* '}'
		
		<Import-Block>			= import string
		
		<Language-Inherits>		= ':' identifier (',' identifier)*
		
		//
		// The language block
		//
		
		<Language-Definition>	= <Lexer-Symbols-Definition>
								| <Lexer-Definition>
								| <Ignore-Definition>
								| <Weak-Symbols-Definition>
								| <Keywords-Definition>
								| <Grammar-Definition>
								
		//
		// Basic language items
		//
		
		<Lexer-Symbols-Definition>	= lexer-symbols '{' (<Lexeme-Definition>)* '}'
		
		<Lexer-Definition> 			= lexer '{' (<Lexeme-Definition>)* '}'
		
		<Ignore-Definition>			= ignore '{' (<Lexeme-Definition> | identifier)* '}'
		
		<Keywords-Definition>		= keywords '{' (<Keyword-Definition>)* '}'
		
		<Keyword-Definition>		= identifier
									| identifier '=' (regex | string | character)
		
		<Weak-Symbols-Definition>	= weak-lexer '{' (<Lexeme-Definition>)* '}'
		
		<Lexeme-Definition> 		= identifier '=' (regex | string | character)
									| identifier '=' identifier '.' identifier
		
		//
		// Defining grammars
		//
		
		<Grammar-Definition>		= grammar '{' (<Nonterminal-Definition>)* '}'
		
		<Nonterminal-Definition>	= [=> nonterminal '='] nonterminal '=' <Production> ('|' <Production>)*
									| nonterminal "+=" <Production> ('|' <Production>)*
									| replace <Nonterminal> '=' <Production> ('|' <Production>)*
		
		// Top level is just a simple EBNF term, as the '|' operator creates a new production at this point
		<Production>				= (Simple-Ebnf-Item)*
		
		<Ebnf-Item>					= <Simple-Ebnf-Item>
									| <Simple-Ebnf-Item> '|' <Ebnf-Item>
		
		<Simple-Ebnf-Item>			= <Nonterminal>
									| <Terminal>
									| <Guard>
									| <Simple-Ebnf-Item> '*'
									| <Simple-Ebnf-Item> '+'
									| <Simple-Ebnf-Item> '?'
									| '(' <Ebnf-Item>* ')'
		
		<Guard>						= "[=>" (<Ebnf-Item>)* ']'
		
		<Nonterminal>				= nonterminal
									| identifier '.' nonterminal
		
		<Terminal>					= <Basic-Terminal>
									| identifier '.' <Basic-Terminal>
		
		<Basic-Terminal>			= identifier
									| string
									| character
	}
}
